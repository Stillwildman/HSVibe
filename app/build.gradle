plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

def static getDate() {
    def date = new Date()
    return date.format('yyyyMMdd')
}

def static getKeystoreProperty(def projectName, def projectFile) {
    def keystoreProperties = null
    if (projectFile.exists() && projectFile.canRead()) {
        keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(projectFile))
    }
    if (keystoreProperties == null) {
        System.err.println '' + projectName + ' not exist or can not read or load failed!'
    }
    return keystoreProperties
}

def debugProperties = getKeystoreProperty('debug', rootProject.file('../commonDebug.properties'))
def releaseProperties = getKeystoreProperty('release', rootProject.file('../hsvieRelease.properties'))

android {
    compileSdkVersion 32
    buildToolsVersion "33.0.0"

    defaultConfig {
        applicationId "com.hsvibe"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 55
        versionName "3.0.5"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        // Used by Room, to test migrations
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        setProperty("archivesBaseName", "HSVibe_" + getDate() + "_v" + versionName)
        //project.ext.set("archivesBaseName", "HSVibe_" + getDate() + "_v" + versionName)
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            //applicationIdSuffix ".dev"
            //versionNameSuffix '-dev'

        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    flavorDimensions 'EnvType'

    productFlavors {
        formal {
            dimension "EnvType"
            buildConfigField "boolean", "IS_FORMAL_ENV", "true"
            manifestPlaceholders = [appName: "H\'S VIBE"]
        }
        dev {
            dimension "EnvType"
            applicationIdSuffix ".dev"
            versionNameSuffix '-dev'
            buildConfigField "boolean", "IS_FORMAL_ENV", "false"
            manifestPlaceholders = [appName: "H\'S VIBE dev"]
        }
    }

    signingConfigs {
        release {
            if (releaseProperties != null) {
                keyAlias releaseProperties['keyAlias']
                keyPassword releaseProperties['keyPassword']
                storeFile file(releaseProperties['storeFile'])
                storePassword releaseProperties['storePassword']
            }
            v1SigningEnabled true
            v2SigningEnabled true
        }
        debug {
            if (debugProperties != null) {
                keyAlias debugProperties['keyAlias']
                keyPassword debugProperties['keyPassword']
                storeFile file(debugProperties['storeFile'])
                storePassword debugProperties['storePassword']
            }
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    implementation "androidx.fragment:fragment-ktx:1.4.1"

    // Biometrics
    implementation 'androidx.biometric:biometric:1.1.0'

    // Google API
    implementation "com.google.android.gms:play-services-location:20.0.0"

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:30.1.0')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.google.firebase:firebase-dynamic-links-ktx'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'

    // Gson
    implementation 'com.google.code.gson:gson:2.8.9'

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrolfit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrolfit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrolfit_version"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.20'
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttp_version"
    implementation "com.squareup.okhttp3:okhttp-brotli:$okHttp_version"

    // Paging
    implementation 'androidx.paging:paging-runtime-ktx:3.1.1'

    // ViewModel
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    annotationProcessor 'androidx.lifecycle:lifecycle-common-java8:2.4.1'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Code Scanner
    implementation 'com.budiyev.android:code-scanner:2.1.0'

    // ZXing
    implementation('com.journeyapps:zxing-android-embedded:4.3.0') { transitive = false }
    implementation 'com.google.zxing:core:3.4.0'

    // EventBus
    implementation 'org.greenrobot:eventbus:3.3.1'
}